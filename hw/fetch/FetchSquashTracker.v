//========================================================================
// FetchSquashTracker.v
//========================================================================
// Tracks the number of in-flight fetch requests, the number of stale
// responses that must be drained after a ctrl_flow, and ctrl_flow restart
// state. Produces a should_drop signal that tells the datapath to
// discard incoming memory responses, and per-lane restart information
// for the first valid fetch block after a ctrl_flow.

`ifndef HW_FETCH_FETCHSQUASHTRACKER_V
`define HW_FETCH_FETCHSQUASHTRACKER_V

`include "intf/ControlFlowNotif.v"

module FetchSquashTracker
#(
  parameter p_max_in_flight = 16,
  parameter p_num_fe_lanes  = 2,

  // Derived parameters (do not override)
  parameter p_flight_bits   = $clog2(p_max_in_flight) + 1 > $clog2(p_num_fe_lanes) + 1
                               ? $clog2(p_max_in_flight) + 1
                               : $clog2(p_num_fe_lanes) + 1,
  parameter p_lane_idx_bits = p_num_fe_lanes > 1
                               ? $clog2(p_num_fe_lanes) : 1
)
(
  input  logic        clk,
  input  logic        rst,

  //----------------------------------------------------------------------
  // Control Flow Interface
  //----------------------------------------------------------------------

  ControlFlowNotif.sub ctrl_flow,

  //----------------------------------------------------------------------
  // Control inputs
  //----------------------------------------------------------------------

  input  logic [p_lane_idx_bits-1:0] ctrl_flow_lane_offset,
  input  logic                       memreq_xfer,
  input  logic                       D_xfer_all,
  input  logic                       mem_resp_val,

  //----------------------------------------------------------------------
  // Outputs
  //----------------------------------------------------------------------

  output logic [p_flight_bits-1:0]   num_in_flight,
  output logic [p_flight_bits-1:0]   num_to_squash,
  output logic                       should_drop,
  output logic                       needs_ctrl_flow_restart,
  output logic [p_lane_idx_bits-1:0] ctrl_flow_restart_offset
);

  localparam logic [p_flight_bits-1:0] c_num_fe_lanes = p_num_fe_lanes;

  //----------------------------------------------------------------------
  // Number of in-flight requests
  //----------------------------------------------------------------------

  logic [p_flight_bits-1:0] num_in_flight_next;

  always_ff @( posedge clk ) begin
    if( rst )
      num_in_flight <= '0;
    else
      num_in_flight <= num_in_flight_next;
  end

  always_comb begin
    num_in_flight_next = num_in_flight;

    // All in-flight messages should be ctrl_flowed
    if( ctrl_flow.redirect_val )
      num_in_flight_next = 0;

    // Add in-flight fetch block (c_num_fe_lanes messages) for each request to
    // imem that goes out
    if( memreq_xfer & !D_xfer_all )
      num_in_flight_next = num_in_flight_next + c_num_fe_lanes;

    // Response that comes back and is consumed by D
    if( D_xfer_all & !memreq_xfer )
      num_in_flight_next = num_in_flight_next - c_num_fe_lanes;
  end

  //----------------------------------------------------------------------
  // Number of in-flight requests to ctrl_flow
  //----------------------------------------------------------------------

  logic [p_flight_bits-1:0] num_to_squash_next;

  always_ff @( posedge clk ) begin
    if( rst )
      num_to_squash <= '0;
    else
      num_to_squash <= num_to_squash_next;
  end

  always_comb begin
    num_to_squash_next = num_to_squash;

    // If ctrl_flowing, the next number of instructions to ctrl_flow is incremented by
    // the number of instructions in flight
    if( ctrl_flow.redirect_val )
      num_to_squash_next = num_to_squash_next + num_in_flight;

    // Drain a stale fetch block when memory delivers a response and we have
    // some to ctrl_flow (should_drop is guaranteed true, so mem.resp_rdy is high)
    if( mem_resp_val & ( num_to_squash_next > 0 ) )
      num_to_squash_next = num_to_squash_next - c_num_fe_lanes;
  end

  assign should_drop = ctrl_flow.redirect_val | ( num_to_squash > 0 );

  //----------------------------------------------------------------------
  // CtrlFlow restart state
  //----------------------------------------------------------------------
  // After a ctrl_flow, the first valid fetch block may start at a mid-block
  // lane. needs_ctrl_flow_restart tracks whether the next good block needs
  // lane masking, and ctrl_flow_restart_offset records which lane to start
  // from.

  always_ff @( posedge clk ) begin
    if( rst )
      needs_ctrl_flow_restart <= 1'b0;
    else if( ctrl_flow.redirect_val )
      needs_ctrl_flow_restart <= 1'b1;
    else if( D_xfer_all )
      needs_ctrl_flow_restart <= 1'b0;
  end

  always_ff @( posedge clk ) begin
    if( rst )
      ctrl_flow_restart_offset <= '0;
    else if( ctrl_flow.redirect_val )
      ctrl_flow_restart_offset <= ctrl_flow_lane_offset;
  end

endmodule

`endif // HW_FETCH_FETCHSQUASHTRACKER_V
