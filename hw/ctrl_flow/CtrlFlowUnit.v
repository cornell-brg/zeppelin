//========================================================================
// CtrlFlowUnit.v
//========================================================================
// Age-based control flow arbiter with a decoupled grant for the
// decode-issue unit.  Produces two grant outputs:
//
//   gnt       – full arbitration across all sources (for fetch, etc.)
//   gnt_excl  – arbitration excluding the DIU source (for the DIU's
//               own ctrl_flow_sub, breaking the combinational loop when
//               DIUCtrlFlowPublisher is combinational)
//
// Arbitration is based on redirect_val (ctrl_flow events). bp_update_val
// and branch resolution fields pass through from the winning source.
//
// When p_num_arb == 2, gnt_excl is a zero-cost passthrough of the
// single non-DIU source.

`ifndef HW_CTRL_FLOW_CTRLFLOWUNIT_V
`define HW_CTRL_FLOW_CTRLFLOWUNIT_V

`include "hw/util/SSSeqAge.v"
`include "intf/ControlFlowNotif.v"
`include "intf/CommitNotif.v"

//------------------------------------------------------------------------
// CtrlFlowUnitHelper
//------------------------------------------------------------------------
// Pairwise age-based arbiter.  Selects the older of two control flow
// notifications based on redirect_val, using a circular sequence-number
// comparison.

module CtrlFlowUnitHelper #(
  parameter p_seq_num_bits = 5
)(
  input  logic [p_seq_num_bits-1:0] arb0_seq_num,
  input  logic               [31:0] arb0_pc,
  input  logic               [31:0] arb0_target,
  input  logic                      arb0_taken,
  input  logic                      arb0_bp_update_val,
  input  logic                      arb0_redirect_val,
  input  logic [p_seq_num_bits-1:0] arb1_seq_num,
  input  logic               [31:0] arb1_pc,
  input  logic               [31:0] arb1_target,
  input  logic                      arb1_taken,
  input  logic                      arb1_bp_update_val,
  input  logic                      arb1_redirect_val,

  output logic [p_seq_num_bits-1:0] gnt_seq_num,
  output logic               [31:0] gnt_pc,
  output logic               [31:0] gnt_target,
  output logic                      gnt_taken,
  output logic                      gnt_bp_update_val,
  output logic                      gnt_redirect_val,

  input  logic [p_seq_num_bits-1:0] oldest_seq_num
);

  logic arb0_is_older;
  assign arb0_is_older = ( arb0_seq_num < arb1_seq_num   ) ^
                         ( arb0_seq_num < oldest_seq_num ) ^
                         ( arb1_seq_num < oldest_seq_num );

  always_comb begin
    if( !arb0_redirect_val ) begin
      gnt_seq_num       = arb1_seq_num;
      gnt_pc            = arb1_pc;
      gnt_target        = arb1_target;
      gnt_taken         = arb1_taken;
      gnt_bp_update_val = arb1_bp_update_val;
      gnt_redirect_val  = arb1_redirect_val;
    end else if( !arb1_redirect_val ) begin
      gnt_seq_num       = arb0_seq_num;
      gnt_pc            = arb0_pc;
      gnt_target        = arb0_target;
      gnt_taken         = arb0_taken;
      gnt_bp_update_val = arb0_bp_update_val;
      gnt_redirect_val  = arb0_redirect_val;
    end else if( arb0_is_older ) begin
      gnt_seq_num       = arb0_seq_num;
      gnt_pc            = arb0_pc;
      gnt_target        = arb0_target;
      gnt_taken         = arb0_taken;
      gnt_bp_update_val = arb0_bp_update_val;
      gnt_redirect_val  = arb0_redirect_val;
    end else begin
      gnt_seq_num       = arb1_seq_num;
      gnt_pc            = arb1_pc;
      gnt_target        = arb1_target;
      gnt_taken         = arb1_taken;
      gnt_bp_update_val = arb1_bp_update_val;
      gnt_redirect_val  = arb1_redirect_val;
    end
  end

endmodule

//------------------------------------------------------------------------
// CtrlFlowUnit
//------------------------------------------------------------------------

module CtrlFlowUnit #(
  parameter p_num_arb       = 2,
  parameter p_diu_idx       = 0,
  parameter p_num_be_lanes  = 2,
  parameter p_seq_num_bits  = 5,
  parameter p_num_phys_regs = 36
) (
  input  logic clk,
  input  logic rst,

  //----------------------------------------------------------------------
  // Notifications to arbitrate between
  //----------------------------------------------------------------------

  ControlFlowNotif.sub arb [p_num_arb],

  //----------------------------------------------------------------------
  // Full arbitrated notification (all sources)
  //----------------------------------------------------------------------

  ControlFlowNotif.pub gnt,

  //----------------------------------------------------------------------
  // Arbitrated notification excluding DIU (for DIU's ctrl_flow_sub)
  //----------------------------------------------------------------------

  ControlFlowNotif.pub gnt_excl,

  //----------------------------------------------------------------------
  // Commit to track age comparison
  //----------------------------------------------------------------------

  CommitNotif.sub commit [p_num_be_lanes]
);

  //----------------------------------------------------------------------
  // Single SSSeqAge instance for age tracking (shared)
  //----------------------------------------------------------------------

  logic [p_seq_num_bits-1:0] oldest_seq_num;

  SSSeqAge #(
    .p_num_be_lanes  (p_num_be_lanes),
    .p_seq_num_bits  (p_seq_num_bits),
    .p_num_phys_regs (p_num_phys_regs)
  ) seq_age (
    .clk            (clk),
    .rst            (rst),
    .oldest_seq_num (oldest_seq_num),
    .commit         (commit)
  );

  //----------------------------------------------------------------------
  // Full grant: binary-tree arbitration across all sources
  //----------------------------------------------------------------------

  localparam p_full_levels = $clog2( p_num_arb );
  localparam p_full_intf   = ( 2 ** ( p_full_levels + 1 ) ) - 1;

  generate
    if( p_num_arb == 1 ) begin: FULL_BASE
      assign gnt.seq_num       = arb[0].seq_num;
      assign gnt.pc            = arb[0].pc;
      assign gnt.target        = arb[0].target;
      assign gnt.taken         = arb[0].taken;
      assign gnt.bp_update_val = arb[0].bp_update_val;
      assign gnt.redirect_val  = arb[0].redirect_val;

    end else begin: FULL_TREE
      logic [p_seq_num_bits-1:0] full_seq_num       [p_full_intf] /* verilator split_var */;
      logic               [31:0] full_pc            [p_full_intf] /* verilator split_var */;
      logic               [31:0] full_target        [p_full_intf] /* verilator split_var */;
      logic                      full_taken         [p_full_intf] /* verilator split_var */;
      logic                      full_bp_update_val [p_full_intf] /* verilator split_var */;
      logic                      full_redirect_val  [p_full_intf] /* verilator split_var */;

      for( genvar i = 0; i < 2 ** p_full_levels; i++ ) begin: FULL_INIT
        if( i < p_num_arb ) begin
          assign full_seq_num[i]       = arb[i].seq_num;
          assign full_pc[i]            = arb[i].pc;
          assign full_target[i]        = arb[i].target;
          assign full_taken[i]         = arb[i].taken;
          assign full_bp_update_val[i] = arb[i].bp_update_val;
          assign full_redirect_val[i]  = arb[i].redirect_val;
        end else begin
          assign full_seq_num[i]       = '0;
          assign full_pc[i]            = '0;
          assign full_target[i]        = '0;
          assign full_taken[i]         = 1'b0;
          assign full_bp_update_val[i] = 1'b0;
          assign full_redirect_val[i]  = 1'b0;
        end
      end

      for( genvar i = 0; i < p_full_levels; i++ ) begin: FULL_LEVEL
        for( genvar j = 0; j < (2 ** i); j++ ) begin: FULL_NODE
          CtrlFlowUnitHelper #(
            .p_seq_num_bits (p_seq_num_bits)
          ) helper (
            .arb0_seq_num       ( full_seq_num      [p_full_intf - (2*j) - (2*(2**i))    ] ),
            .arb0_pc            ( full_pc           [p_full_intf - (2*j) - (2*(2**i))    ] ),
            .arb0_target        ( full_target       [p_full_intf - (2*j) - (2*(2**i))    ] ),
            .arb0_taken         ( full_taken        [p_full_intf - (2*j) - (2*(2**i))    ] ),
            .arb0_bp_update_val ( full_bp_update_val[p_full_intf - (2*j) - (2*(2**i))    ] ),
            .arb0_redirect_val  ( full_redirect_val [p_full_intf - (2*j) - (2*(2**i))    ] ),
            .arb1_seq_num       ( full_seq_num      [p_full_intf - (2*j) - (2*(2**i)) - 1] ),
            .arb1_pc            ( full_pc           [p_full_intf - (2*j) - (2*(2**i)) - 1] ),
            .arb1_target        ( full_target       [p_full_intf - (2*j) - (2*(2**i)) - 1] ),
            .arb1_taken         ( full_taken        [p_full_intf - (2*j) - (2*(2**i)) - 1] ),
            .arb1_bp_update_val ( full_bp_update_val[p_full_intf - (2*j) - (2*(2**i)) - 1] ),
            .arb1_redirect_val  ( full_redirect_val [p_full_intf - (2*j) - (2*(2**i)) - 1] ),
            .gnt_seq_num        ( full_seq_num      [p_full_intf -     j  -    (2**i)     ] ),
            .gnt_pc             ( full_pc           [p_full_intf -     j  -    (2**i)     ] ),
            .gnt_target         ( full_target       [p_full_intf -     j  -    (2**i)     ] ),
            .gnt_taken          ( full_taken        [p_full_intf -     j  -    (2**i)     ] ),
            .gnt_bp_update_val  ( full_bp_update_val[p_full_intf -     j  -    (2**i)     ] ),
            .gnt_redirect_val   ( full_redirect_val [p_full_intf -     j  -    (2**i)     ] ),
            .oldest_seq_num     ( oldest_seq_num )
          );
        end
      end

      assign gnt.seq_num       = full_seq_num      [p_full_intf - 1];
      assign gnt.pc            = full_pc           [p_full_intf - 1];
      assign gnt.target        = full_target       [p_full_intf - 1];
      assign gnt.taken         = full_taken        [p_full_intf - 1];
      assign gnt.bp_update_val = full_bp_update_val[p_full_intf - 1];
      assign gnt.redirect_val  = full_redirect_val [p_full_intf - 1];
    end
  endgenerate

  //----------------------------------------------------------------------
  // Excluded grant: arbitrate all sources except arb[p_diu_idx]
  //----------------------------------------------------------------------

  localparam p_excl_arb    = p_num_arb - 1;
  localparam p_excl_levels = $clog2( p_excl_arb > 0 ? p_excl_arb : 1 );
  localparam p_excl_intf   = ( 2 ** ( p_excl_levels + 1 ) ) - 1;

  generate
    if( p_num_arb == 2 ) begin: EXCL_PASSTHRU
      // Only one non-DIU source — direct passthrough, zero hardware.
      localparam p_other_idx = (p_diu_idx == 0) ? 1 : 0;

      assign gnt_excl.seq_num       = arb[p_other_idx].seq_num;
      assign gnt_excl.pc            = arb[p_other_idx].pc;
      assign gnt_excl.target        = arb[p_other_idx].target;
      assign gnt_excl.taken         = arb[p_other_idx].taken;
      assign gnt_excl.bp_update_val = arb[p_other_idx].bp_update_val;
      assign gnt_excl.redirect_val  = arb[p_other_idx].redirect_val;

    end else begin: EXCL_TREE
      // Pack non-DIU sources into flat arrays.
      logic [p_seq_num_bits-1:0] excl_seq_num       [p_excl_intf] /* verilator split_var */;
      logic               [31:0] excl_pc            [p_excl_intf] /* verilator split_var */;
      logic               [31:0] excl_target        [p_excl_intf] /* verilator split_var */;
      logic                      excl_taken         [p_excl_intf] /* verilator split_var */;
      logic                      excl_bp_update_val [p_excl_intf] /* verilator split_var */;
      logic                      excl_redirect_val  [p_excl_intf] /* verilator split_var */;

      for( genvar i = 0; i < p_num_arb; i++ ) begin: EXCL_CONNECT
        if( i < p_diu_idx ) begin
          assign excl_seq_num[i]       = arb[i].seq_num;
          assign excl_pc[i]            = arb[i].pc;
          assign excl_target[i]        = arb[i].target;
          assign excl_taken[i]         = arb[i].taken;
          assign excl_bp_update_val[i] = arb[i].bp_update_val;
          assign excl_redirect_val[i]  = arb[i].redirect_val;
        end else if( i > p_diu_idx ) begin
          assign excl_seq_num[i-1]       = arb[i].seq_num;
          assign excl_pc[i-1]            = arb[i].pc;
          assign excl_target[i-1]        = arb[i].target;
          assign excl_taken[i-1]         = arb[i].taken;
          assign excl_bp_update_val[i-1] = arb[i].bp_update_val;
          assign excl_redirect_val[i-1]  = arb[i].redirect_val;
        end
      end

      // Zero-fill unused leaf slots.
      for( genvar i = p_excl_arb; i < 2 ** p_excl_levels; i++ ) begin: EXCL_ZERO
        assign excl_seq_num[i]       = '0;
        assign excl_pc[i]            = '0;
        assign excl_target[i]        = '0;
        assign excl_taken[i]         = 1'b0;
        assign excl_bp_update_val[i] = 1'b0;
        assign excl_redirect_val[i]  = 1'b0;
      end

      // Binary-tree arbitration.
      for( genvar i = 0; i < p_excl_levels; i++ ) begin: EXCL_LEVEL
        for( genvar j = 0; j < (2 ** i); j++ ) begin: EXCL_NODE
          CtrlFlowUnitHelper #(
            .p_seq_num_bits (p_seq_num_bits)
          ) helper (
            .arb0_seq_num       ( excl_seq_num      [p_excl_intf - (2*j) - (2*(2**i))    ] ),
            .arb0_pc            ( excl_pc           [p_excl_intf - (2*j) - (2*(2**i))    ] ),
            .arb0_target        ( excl_target       [p_excl_intf - (2*j) - (2*(2**i))    ] ),
            .arb0_taken         ( excl_taken        [p_excl_intf - (2*j) - (2*(2**i))    ] ),
            .arb0_bp_update_val ( excl_bp_update_val[p_excl_intf - (2*j) - (2*(2**i))    ] ),
            .arb0_redirect_val  ( excl_redirect_val [p_excl_intf - (2*j) - (2*(2**i))    ] ),
            .arb1_seq_num       ( excl_seq_num      [p_excl_intf - (2*j) - (2*(2**i)) - 1] ),
            .arb1_pc            ( excl_pc           [p_excl_intf - (2*j) - (2*(2**i)) - 1] ),
            .arb1_target        ( excl_target       [p_excl_intf - (2*j) - (2*(2**i)) - 1] ),
            .arb1_taken         ( excl_taken        [p_excl_intf - (2*j) - (2*(2**i)) - 1] ),
            .arb1_bp_update_val ( excl_bp_update_val[p_excl_intf - (2*j) - (2*(2**i)) - 1] ),
            .arb1_redirect_val  ( excl_redirect_val [p_excl_intf - (2*j) - (2*(2**i)) - 1] ),
            .gnt_seq_num        ( excl_seq_num      [p_excl_intf -     j  -    (2**i)     ] ),
            .gnt_pc             ( excl_pc           [p_excl_intf -     j  -    (2**i)     ] ),
            .gnt_target         ( excl_target       [p_excl_intf -     j  -    (2**i)     ] ),
            .gnt_taken          ( excl_taken        [p_excl_intf -     j  -    (2**i)     ] ),
            .gnt_bp_update_val  ( excl_bp_update_val[p_excl_intf -     j  -    (2**i)     ] ),
            .gnt_redirect_val   ( excl_redirect_val [p_excl_intf -     j  -    (2**i)     ] ),
            .oldest_seq_num     ( oldest_seq_num )
          );
        end
      end

      assign gnt_excl.seq_num       = excl_seq_num      [p_excl_intf - 1];
      assign gnt_excl.pc            = excl_pc           [p_excl_intf - 1];
      assign gnt_excl.target        = excl_target       [p_excl_intf - 1];
      assign gnt_excl.taken         = excl_taken        [p_excl_intf - 1];
      assign gnt_excl.bp_update_val = excl_bp_update_val[p_excl_intf - 1];
      assign gnt_excl.redirect_val  = excl_redirect_val [p_excl_intf - 1];
    end
  endgenerate

  //----------------------------------------------------------------------
  // Linetracing
  //----------------------------------------------------------------------

`ifndef SYNTHESIS
  function int ceil_div_4( int val );
    return (val / 4) + ((val % 4) > 0 ? 1 : 0);
  endfunction

  int str_len;
  assign str_len = ceil_div_4(p_seq_num_bits) + 1 +
                   ceil_div_4(32);

  function string trace( int trace_level );
    if( trace_level > 0 ) begin
      if( gnt.redirect_val )
        trace = $sformatf("%h:%h", gnt.seq_num, gnt.target);
      else
        trace = {str_len{" "}};
    end else begin
      if( gnt.redirect_val )
        trace = $sformatf("%h", gnt.seq_num);
      else
        trace = {(ceil_div_4(p_seq_num_bits)){" "}};
    end
  endfunction
`endif

endmodule

`endif // HW_CTRL_FLOW_CTRLFLOWUNIT_V
